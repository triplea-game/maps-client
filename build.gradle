plugins {
    id 'java'
    id 'pmd'
    id 'maven-publish'
    id 'io.freefair.lombok' version '6.5.1'
    id 'com.diffplug.spotless' version '6.12.0'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'org.triplea'
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

def static revisionCount() {
    ['sh', '-c', 'git rev-list --count HEAD'].execute().text.trim()
}
version = "1.0." + revisionCount()

jar {
    archivesBaseName = 'org.triplea.maps-client'
}

repositories {
    mavenCentral()
}

shadowJar {
    classifier = ''
}

publishing {
    repositories {
        maven {
            name = 'GitHubPackages'
            url = uri('https://maven.pkg.github.com/triplea-game/maps-client')
            credentials {
                username = System.getenv('GITHUB_ACTOR')
                password = System.getenv('GITHUB_TOKEN')
            }
        }
    }
    publications {
        shadow(MavenPublication) {
            artifact shadowJar
            pom {
                packaging 'jar'
            }
        }
    }
}

test {
    useJUnitPlatform()
}

tasks.withType(Test).configureEach {
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }
}

spotless {
    java {
        googleJavaFormat()
        removeUnusedImports()
        endWithNewline()
        indentWithSpaces()
        trimTrailingWhitespace()
    }
}

pmd {
    consoleOutput = true
    ruleSetFiles = files(rootProject.file('.build/pmd.xml'))
    ruleSets = []
    incrementalAnalysis = true
}


dependencies {
    implementation 'ch.qos.logback:logback-classic:1.4.5'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'io.github.openfeign:feign-core:12.1'
    implementation 'com.netflix.feign:feign-jackson:8.18.0'

    testImplementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.14.1'
    testImplementation 'com.github.tomakehurst:wiremock:2.27.2'
    testImplementation 'org.hamcrest:java-hamcrest:2.0.0.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.1'
    testImplementation 'ru.lanwen.wiremock:wiremock-junit5:1.3.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.9.1'
}
